plugins {
    //id 'checkstyle'
    id 'application'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.23.1'
}
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
mainClassName = 'edu.wpi.teamname.Main'

//TestFX doesn't play nice with macOS
String osName = System.getProperty("os.name").toLowerCase();
if (osName.contains("mac")) {
   project.setProperty("headless", "true")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            [group: 'com.jfoenix', name: 'jfoenix', version: '8.0.9'],

            // You may comment out the database dependency you do not use
            [group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'],
            [group: 'org.apache.derby',name: 'derby', version: '10.14.2.0'],
    )

    compileOnly(
            [group: 'org.projectlombok', name: 'lombok', version: '1.18.8']
    )

    annotationProcessor(
            [group: 'org.projectlombok', name: 'lombok', version: '1.18.8']
    )

    testImplementation(
            [group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.0'],
            [group: 'org.testfx', name: 'testfx-junit5', version: '4.0.15-alpha']
    )
    testRuntimeOnly(
            [group: 'org.testfx', name: 'openjfx-monocle', version: '8u76-b04']
    )
}

test {
    useJUnitPlatform()
    jvmArgs "-Dheadless=${project.hasProperty('headless') ? project.headless : false}"
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
            excludes = [
              'edu.wpi.teamname.App',
              'edu.wpi.teamname.Main'
            ]
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.25
            }
            excludes = [
                    'edu.wpi.teamname.App',
                    'edu.wpi.teamname.Main'
            ]
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.25
            }
            excludes = [
                    'edu.wpi.teamname.App',
                    'edu.wpi.teamname.Main'
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport

// uncomment to enable spotless checking
//spotless {
//    java {
//        googleJavaFormat()
//    }
//}
